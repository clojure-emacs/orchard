S = <#'[^"]'*> traces causes <#"."*>

traces = trace+
trace = (frame+ <more>) / frame

frame = <whitespace>? class <slash> method frame-info <newline>
<frame-info> = <whitespace> (<'REPL Input'> | frame-location)
<frame-location> = file <double-colon> <whitespace> number (<whitespace> frame-repeats)?
<frame-repeats> = <lparen> <'repeats'> <whitespace> times <rparen>

causes = cause+
cause = type <double-colon> <whitespace> message <newline> data?
message = #"."+

data = data-entry+
data-entry = <whitespace> data-key <double-colon> <whitespace> data-value <newline>
data-key = simple-name
data-value = #"."+

type = class
class = (simple-name (dot simple-name)+)
file = simple-name (dot simple-name)
method = simple-name (slash simple-name)*
more = <whitespace> <dot> <dot> <dot> <newline>
<times> = number <whitespace> <'time'> <'s'>?

<digit> = #'[0-9]'
<dot> = '.'
<letter> = #'[a-zA-Z]'
<slash> = '/'

double-colon = ':'
newline = #"[\n\r]"
number = '-'? digit+
lparen = '('
rparen = ')'
simple-name = #"[a-zA-Z0-9_$*-]"+
whitespace = #"\s+"
